# Check compilers and version number
#  Liao 11/25/2009
# This is translated from config/choose-backend-compiler.m4
#
# use the results to setup rose_config.h later on
# --------check C compiler -----------------------
include (CMakeDetermineCCompiler)

# Support for Visual Studio is handled in a separate file
if(WIN32)
  include(roseWindowsSupport)
  return()
endif()

if("${CMAKE_C_COMPILER_ID}" STREQUAL "GNU")
  if(NOT BACKEND_C_COMPILER)
    set(BACKEND_C_COMPILER  ${CMAKE_C_COMPILER})
  endif()
  execute_process(
    COMMAND ${BACKEND_C_COMPILER} -dumpversion
    COMMAND cut -d. -f1
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER)
  # for unknown reason, some tail control character will show up in OUTPUT_VARIABLE
  # We use regex to fill out non numeric characters to get the right numbers
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER ${BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER})
  if(VERBOSE)
    message("CMAKE_C_COMPILER= ${CMAKE_C_COMPILER}^")
  endif()

  execute_process(
    COMMAND basename ${BACKEND_C_COMPILER}
    OUTPUT_VARIABLE BACKEND_C_COMPILER_NAME_WITHOUT_PATH)
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_C_COMPILER_NAME_WITHOUT_PATH ${BACKEND_C_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_C_COMPILER_NAME_WITHOUT_PATH=${BACKEND_C_COMPILER_NAME_WITHOUT_PATH}^")
  endif()

  execute_process(
    COMMAND ${BACKEND_C_COMPILER} -dumpversion
    COMMAND cut -d. -f2
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MINOR_VERSION_NUMBER ${BACKEND_C_COMPILER_MINOR_VERSION_NUMBER})
  execute_process(
    COMMAND ${BACKEND_C_COMPILER} -dumpversion
    COMMAND cut -d. -f3
    OUTPUT_VARIABLE BACKEND_C_COMPILER_PATCH_LEVEL_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_PATCH_LEVEL_NUMBER ${BACKEND_C_COMPILER_PATCH_LEVEL_NUMBER})

elseif("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang")
  if(NOT BACKEND_C_COMPILER)
    set(BACKEND_C_COMPILER  ${CMAKE_C_COMPILER})
  endif()
  execute_process(
    COMMAND basename ${BACKEND_C_COMPILER}
    OUTPUT_VARIABLE BACKEND_C_COMPILER_NAME_WITHOUT_PATH)
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_C_COMPILER_NAME_WITHOUT_PATH ${BACKEND_C_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_C_COMPILER_NAME_WITHOUT_PATH=${BACKEND_C_COMPILER_NAME_WITHOUT_PATH}^")
  endif()

  execute_process(
    COMMAND ${BACKEND_C_COMPILER} --version
    COMMAND grep -Po "(?<=version )[^;]+"
    COMMAND cut -d. -f1
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER  ${BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER})
  execute_process(
    COMMAND ${BACKEND_C_COMPILER} --version
    COMMAND grep -Po "(?<=version )[^;]+"
    COMMAND cut -d. -f2
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MINOR_VERSION_NUMBER  ${BACKEND_C_COMPILER_MINOR_VERSION_NUMBER})
elseif("${CMAKE_C_COMPILER_ID}" STREQUAL "AppleClang")
  if(NOT BACKEND_C_COMPILER)
    set(BACKEND_C_COMPILER  ${CMAKE_C_COMPILER})
  endif()
  execute_process(
    COMMAND basename ${BACKEND_C_COMPILER}
    OUTPUT_VARIABLE BACKEND_C_COMPILER_NAME_WITHOUT_PATH)
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_C_COMPILER_NAME_WITHOUT_PATH ${BACKEND_C_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_C_COMPILER_NAME_WITHOUT_PATH=${BACKEND_C_COMPILER_NAME_WITHOUT_PATH}^")
  endif()

  execute_process(
    COMMAND ${CMAKE_SOURCE_DIR}/config/getAppleClangMajorVersionNumber.sh
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER  ${BACKEND_C_COMPILER_MAJOR_VERSION_NUMBER})
  execute_process(
    COMMAND ${CMAKE_SOURCE_DIR}/config/getAppleClangMinorVersionNumber.sh
    OUTPUT_VARIABLE BACKEND_C_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_C_COMPILER_MINOR_VERSION_NUMBER  ${BACKEND_C_COMPILER_MINOR_VERSION_NUMBER})
endif()

# --------check CXX compiler -----------------------
include(roseCMakeDetermineCXXCompiler)
if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
  if(NOT BACKEND_CXX_COMPILER)
    set(BACKEND_CXX_COMPILER  ${CMAKE_CXX_COMPILER})
  endif()
  execute_process(
    COMMAND ${BACKEND_CXX_COMPILER} -dumpversion
    COMMAND cut -d. -f1
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER  ${BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER})
  execute_process(
    COMMAND basename ${BACKEND_CXX_COMPILER}  
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH)
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER= ${BACKEND_CXX_COMPILER}")
  endif()
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH= ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH}")
  endif()
  
  execute_process(
    COMMAND ${BACKEND_CXX_COMPILER} -dumpversion
    COMMAND cut -d. -f2
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER ${BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER})

elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
  if(NOT BACKEND_CXX_COMPILER)
    set(BACKEND_CXX_COMPILER  ${CMAKE_CXX_COMPILER})
  endif()
  execute_process(
    COMMAND basename ${BACKEND_CXX_COMPILER}  
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH)
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER= ${BACKEND_CXX_COMPILER}")
  endif()
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH= ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH}")
  endif()
  execute_process(
    COMMAND ${BACKEND_CXX_COMPILER} --version
    COMMAND grep -Po "(?<=version )[^;]+"
    COMMAND cut -d. -f1
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER  ${BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER})
  execute_process(
    COMMAND ${BACKEND_CXX_COMPILER} --version
    COMMAND grep -Po "(?<=version )[^;]+"
    COMMAND cut -d. -f2
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER  ${BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER})
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "AppleClang")
  if(NOT BACKEND_CXX_COMPILER)
    set(BACKEND_CXX_COMPILER  ${CMAKE_CXX_COMPILER})
  endif()
  execute_process(
    COMMAND basename ${BACKEND_CXX_COMPILER}  
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH)
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER= ${BACKEND_CXX_COMPILER}")
  endif()
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH})
  if(VERBOSE)
    message("BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH= ${BACKEND_CXX_COMPILER_NAME_WITHOUT_PATH}")
  endif()
  execute_process(
    COMMAND ${CMAKE_SOURCE_DIR}/config/getAppleClangMajorVersionNumber.sh
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER  ${BACKEND_CXX_COMPILER_MAJOR_VERSION_NUMBER})
  execute_process(
    COMMAND ${CMAKE_SOURCE_DIR}/config/getAppleClangMinorVersionNumber.sh
    OUTPUT_VARIABLE BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER)
  string(REGEX MATCH "[0-9]+" BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER  ${BACKEND_CXX_COMPILER_MINOR_VERSION_NUMBER})
endif()

if(enable-fortran)
  # --------check Fortran compiler -----------------------
  include(roseCMakeDetermineFortranCompiler)

  if(NOT BACKEND_FORTRAN_COMPILER)
    set(BACKEND_FORTRAN_COMPILER ${CMAKE_Fortran_COMPILER})
  endif()

  get_filename_component(BACKEND_FORTRAN_COMPILER_NAME_WITHOUT_PATH "${BACKEND_FORTRAN_COMPILER}" NAME)
  string(REGEX MATCH "[a-zA-Z0-9/.+-]+" BACKEND_FORTRAN_COMPILER_NAME_WITHOUT_PATH "${BACKEND_FORTRAN_COMPILER_NAME_WITHOUT_PATH}")

  set(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER "")
  set(BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER "")

  # First try compiler-native queries (GNU supports -dumpfullversion/-dumpversion)
  if("${CMAKE_Fortran_COMPILER_ID}" STREQUAL "GNU")
    if(VERBOSE)
      message("Querying GNU Fortran version via -dumpfullversion")
    endif()
    execute_process(
      COMMAND ${BACKEND_FORTRAN_COMPILER} -dumpfullversion
      OUTPUT_VARIABLE _fortran_version_full
      ERROR_VARIABLE _fortran_version_full_err
      OUTPUT_STRIP_TRAILING_WHITESPACE
      ERROR_STRIP_TRAILING_WHITESPACE
      RESULT_VARIABLE _fortran_full_status)
    if(_fortran_full_status EQUAL 0 AND NOT _fortran_version_full STREQUAL "")
      string(REGEX MATCH "([0-9]+)\\.([0-9]+)" _fortran_version_match "${_fortran_version_full}")
      if(_fortran_version_match)
        set(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER "${CMAKE_MATCH_1}")
        set(BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER "${CMAKE_MATCH_2}")
      endif()
    endif()
  endif()

  # Generic fallback: parse `<major>.<minor>` from `--version`
  if(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER STREQUAL "" OR BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER STREQUAL "")
    execute_process(
      COMMAND ${BACKEND_FORTRAN_COMPILER} --version
      OUTPUT_VARIABLE _fortran_version_stdout
      ERROR_VARIABLE _fortran_version_stderr
      OUTPUT_STRIP_TRAILING_WHITESPACE
      ERROR_STRIP_TRAILING_WHITESPACE
      RESULT_VARIABLE _fortran_version_status)
    set(_fortran_version_combined "${_fortran_version_stdout}\n${_fortran_version_stderr}")
    string(REGEX MATCH "([0-9]+)\\.([0-9]+)" _fortran_generic_match "${_fortran_version_combined}")
    if(_fortran_generic_match)
      set(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER "${CMAKE_MATCH_1}")
      set(BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER "${CMAKE_MATCH_2}")
    endif()
  endif()

  if(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER STREQUAL "" OR BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER STREQUAL "")
    message(WARNING "Could not determine Fortran compiler version from ${BACKEND_FORTRAN_COMPILER}; defaulting to 0.0")
    set(BACKEND_FORTRAN_COMPILER_MAJOR_VERSION_NUMBER 0)
    set(BACKEND_FORTRAN_COMPILER_MINOR_VERSION_NUMBER 0)
  endif()
endif()

if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
  # using Clang
  set(BACKEND_CXX_IS_CLANG_COMPILER 1)
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
  # using GCC
  set(BACKEND_CXX_IS_GNU_COMPILER 1)
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
  # using Intel C++
  set(BACKEND_CXX_IS_INTEL_COMPILER 1)
endif()
